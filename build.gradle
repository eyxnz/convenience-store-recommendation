plugins {
    id 'java'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'groovy'
}

group = 'com.convenience'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    archiveFileName = 'app.jar' // jar 파일 이름 변경
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Spring boot Devtools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Spring Data JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // MariaDB
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // Spring Retry
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aspects'

    // Spock
    testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
    testImplementation 'org.spockframework:spock-spring:2.1-groovy-3.0'

    // 런타임에 클래스 기반 spock mock 을 만들기 위해서 필요
    testImplementation 'net.bytebuddy:byte-buddy:1.12.10'

    // testcontainers
    testImplementation 'org.testcontainers:spock:1.17.1'
    testImplementation 'org.testcontainers:mariadb:1.17.1'

    // Handlebars
    implementation 'pl.allegro.tech.boot:handlebars-spring-boot-starter:0.3.4'

    // https://github.com/seruco/base62
    implementation 'io.seruco.encoding:base62:0.1.3'
}

tasks.named('test') {
    useJUnitPlatform()
}

// $ ./gradlew clean build -PKAKAO_REST_API_KEY={api key 값} 명령어로 전체 테스트 및 빌드하여 jar 파일 생성
processTestResources {
    boolean hasProperty = project.hasProperty("KAKAO_REST_API_KEY")
    System.out.println("Set kakao rest api key: $hasProperty")
    filesMatching('**/application.yml') {
        expand(project.properties)
    }
}